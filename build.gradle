buildscript {
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.0.1'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:2.0'

    }

}

plugins {
    id "io.spring.dependency-management" version "1.0.4.RELEASE"
    id "com.diffplug.gradle.spotless" version "3.10.0"
}

allprojects {
    repositories {
        google()
        jcenter()
    }

    project.apply from: "${rootDir}/constants.gradle"
}

subprojects {
    apply plugin: 'io.spring.dependency-management'
    apply plugin: "com.diffplug.gradle.spotless"

    dependencyManagement {
        imports {
            mavenBom 'org.jboss.aerogear:aerogear-android-sdk-bom:1.1.11'
        }
    }
    spotless {
        java {
            target '**/*.java'
            removeUnusedImports()
            importOrderFile '../codequality/aerogear.importorder'
            eclipse('4.7.1').configFile '../codequality/eclipse-code-style.xml'
        }
    }


    afterEvaluate { project ->
        dependencies {
            lintChecks files('libs/checks.jar')
        }

        android.buildTypes.each { type ->
            type.buildConfigField 'String', 'PROJECT_ROOT', "\"" + project.projectDir.absolutePath + "/\""
        }

    }
}

// We want to expose the SDK version and name to the metrics subproject
project(':core') {
    project.ext {
        versionName = VERSION_NAME
    }
}

